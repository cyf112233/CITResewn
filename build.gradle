plugins.apply 'fabric-loom'
plugins.apply 'io.shcm.shsupercm.fabric.fletchingtable'

version = "${property('mod.version')}+${stonecutter.current.version}"
group = 'shcm.shsupercm.fabric.citresewn'

repositories {
    maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://maven.shedaniel.me/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"
    mappings "net.fabricmc:yarn:1.21.4+build.1:v2"

    modCompileOnly "net.fabricmc:fabric-loader:0.16.9"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:0.114.1+1.21.4"

    modCompileOnly "com.terraformersmc:modmenu:11.0.2"
    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:15.0.140"

    include stonecutter.node.sibling("defaults").project
}

fletchingTable {
    defaultMixinEnvironment = "auto"
}

loom {
    accessWidenerPath = file("../../src/main/resources/citresewn.accesswidener")

    runConfigs.client.ideConfigGenerated = false
    runConfigs.server.ideConfigGenerated = false
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    archivesBaseName = property('mod.jarname')
}

processResources {
    inputs.property 'mod_version', version
    inputs.property 'mod_target_mc', project.property('mod.target-mc')

    filesMatching('fabric.mod.json') {
        expand 'mod_version': version, 'mod_target_mc': project.property('mod.target-mc')
    }
}

jar {
    archiveClassifier.set 'dev'

    from rootProject.file('LICENSE')
}
